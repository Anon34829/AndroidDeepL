apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

ext {
    supportLibraryVersion = "27.1.1"
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.anthony.deepl.openl"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 5
        versionName "2.0.1"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "mode", "service"
    productFlavors {
        withPlayServices {
            dimension "service"
        }
        withoutPlayServices {
            dimension "service"
        }
        deepl {
            dimension "mode"
        }
        openl {
            dimension "mode"
        }
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5'

    // Google & Android libraries
    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    implementation "com.android.support.constraint:constraint-layout:1.1.0-beta5"
    withPlayServicesImplementation('com.crashlytics.sdk.android:crashlytics:2.9.3@aar') {
        transitive = true
    }
    withPlayServicesImplementation 'com.google.firebase:firebase-core:16.0.0'
    withPlayServicesImplementation 'com.google.firebase:firebase-config:16.0.0'

    // Other libraries
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"
    implementation 'com.jakewharton.timber:timber:4.7.0'

    // Tests
    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
}

apply plugin: 'com.google.gms.google-services'